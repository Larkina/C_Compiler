# 
# C expression rules
# 

primary-expression
    identifier      
    constant          
    string-literal  
    ( expression )  

// Левая факторизация
postfix-expression
    primary-expression postfix_f

postfix_f
    [ expression ] postfix_f
    ( ) postfix_f
    ( argument-expression-list ) postfix_f
    . identifier postfix_f
    -> identifier postfix_f
    ++ postfix_f
    -- postfix_f
    e

argument-expression-list
    assignment-expression
    assignment-expression , argument-expression-list

unary-expression
    postfix-expression
    ++ unary-expression
    -- unary-expression
    unary-operator cast-expression

unary-operator
    & 
    * 
    + 
    - 
    ~
    !

cast-expression
    unary-expression
    ( type-name ) cast-expression

multiplicative-expression
    cast-expression
    cast-expression * multiplicative-expression 
    cast-expression / multiplicative-expression
    cast-expression % multiplicative-expression

additive-expression
    multiplicative-expression
    multiplicative-expression + additive-expression
    multiplicative-expression - additive-expression

shift-expression
    additive-expression
    additive-expression >> shift-expression
    additive-expression << shift-expression

relational-expression
    shift-expression
    shift-expression < relational-expression
    shift-expression > relational-expression
    shift-expression <= relational-expression
    shift-expression >= relational-expression

equality-expression
    relational-expression
    relational-expressione == equality-expression
    relational-expression != equality-expression 

AND-expression
    equality-expression
    equality-expression & AND-expression

exclusive-OR-expression
    AND-expression
    AND-expression ^ exclusive-OR-expression

inclusive-OR-expression
    exclusive-OR-expression
    exclusive-OR-expression | inclusive-OR-expression

logical-AND-expression
    inclusive-OR-expression
    inclusive-OR-expression && logical-AND-expression

logical-OR-expression
    logical-AND-expression
    logical-AND-expression || logical-OR-expression

conditional-expression
    logical-OR-expression
    logical-OR-expression ? expression : conditional-expression

assignment-expression
    conditional-expression
    conditional-expression assignment-operator assignment-expression

assignment-operator
    =
    *=
    /=
    %=
    +=
    -=
    <<=
    >>=
    &=
    ^=
    |=

expression
    assignment-expression
    assignment-expression , expression

constant-expression
    conditional-expression

#
# C declaration rules
#

declaration
    declaration-specifiers ;
    declaration-specifiers init-declarator-list ;

declaration-specifiers
    type-specifier
    type-specifier declaration-specifiers

init-declarator-list
    init-declarator
    init-declarator, init-declarator-list 

init-declarator
    declarator
    declarator = initializer

type-specifier
    void
    int
    double
    struct-specifier

struct-specifier
    struct { struct-declaration-list }
    struct identifier { struct-declaration-list }
    struct identifier

struct-declaration-list
    struct-declaration
    struct-declaration-list struct-declaration

struct-declaration
    specifier-qualifier-list struct-declarator-list ;

specifier-qualifier-list
    type-specifier
    type-specifier specifier-qualifier-list 

struct-declarator-list
    struct-declarator
    struct-declarator , struct-declarator-list

struct-declarator
    declarator
    constant-expression
    declarator  constant-expression

declarator
    direct-declarator
    pointer direct-declarator

direct-declarator
    identifier
    ( declarator )
    direct-declarator [ ]
    direct-declarator [ constant-expression ]
    direct-declarator ( )
    direct-declarator ( parameter-type-list )
    direct-declarator ( identifier-list )

pointer
     *
     * pointer

parameter-type-list
    parameter-list

parameter-list
    parameter-declaration
    parameter-list , parameter-declaration

parameter-declaration
    declaration-specifiers declarator
    declaration-specifiers
    declaration-specifiers abstract-declarator

identifier-list
    identifier
    identifier-list , identifier

type-name
    specifier-qualifier-list
    specifier-qualifier-list abstract-declarator

abstract-declarator
    pointer
    direct-abstract-declarator
    pointer direct-abstract-declarator

direct-abstract-declarator-f
    [ ] direct-abstract-declarator-f
    [ constant-expression ] direct-abstract-declarator-f
    ( ) direct-abstract-declarator-f
    ( parametr-type-list ) direct-abstract-declarator-f
    e
 
direct-abstract-declarator
    ( abstract-declarator ) direct-abstract-declarator-f
    [ ] direct-abstract-declarator-f
    [ constant-expression ] direct-abstract-declarator-f
    ( ) direct-abstract-declarator-f
    ( parameter-type-list ) direct-abstract-declarator-f

initializer
    assignment-expression
    { initializer-list }
    { initializer-list , }

initializer-list
    initializer
    initializer-list , initializer

#
# C statement rules
#

statement
    compound-statement
    expression-statement
    selection-statement
    iteration-statement
    jump-statement

compound-statement
    { }
    { declaration-list }
    { statement-list }
    { declaration-list statement-list }

declaration-list
    declaration
    declaration-list declaration

statement-list
    statement
    statement-list statement

expression-statement
    ;
    expression ;

selection-statement
    if ( expression ) statement
    if ( expression ) statement else statement

iteration-statement
    while ( expression ) statement
    do statement while ( expression ) ;
    for (            ;            ;            ) statement
    for (            ;            ; expression ) statement
    for (            ; expression ;            ) statement
    for (            ; expression ; expression ) statement
    for ( expression ;            ;            ) statement
    for ( expression ;            ; expression ) statement
    for ( expression ; expression ;            ) statement
    for ( expression ; expression ; expression ) statement

jump-statement
    continue ;
    break ;
    return ;
    return expression ;

translation-unit
    external-declaration
    translation-unit external-declaration

external-declaration
    function-definition
    declaration

function-definition
                           declarator                  compound-statement
    declaration-specifiers declarator                  compound-statement
                           declarator declaration-list compound-statement
    declaration-specifiers declarator declaration-list compound-statement